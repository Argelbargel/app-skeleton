import org.apache.tools.ant.taskdefs.condition.Os

node {
    version = nodeVersion
    download = true
    workDir = nodePluginWorkDir
    npmWorkDir = file("${workDir}/.npm")
}


def nodeJsInstallDir = "${node.workDir}/node-v${nodeVersion}-${osName != "linux" ? osName : "linux-x64"}"
def cordovaInstallDir = "${nodeJsInstallDir}/lib/node_modules/cordova"

def installCordovaTask = tasks.register("installCordova", NpmTask) {
    group("build setup")
    dependsOn(tasks.named("npmSetup"))

    onlyIf { !file(cordovaInstallDir).exists() }
    args = [ 'install', "cordova@${cordovaVersion}", '-g' ]
}

def createCordovaAppTask = tasks.register("createCordovaApp", NpxTask) {
    group("build setup")

    dependsOn(installCordovaTask)

    onlyIf { !file("./app").exists() }
    command = "cordova"
    args = createCordovaNpxArgs([ "create", "app", project.group, project.name ])
}

def addCordovaAndroidPlatformTask = tasks.register("addCordovaAndroidPlatform", NpxTask) {
    group("cordova")

    onlyIf { !file("./app/platforms/android").exists() }
    mustRunAfter createCordovaAppTask

    command = "cordova"
    args = createCordovaNpxArgs([ "platform", "add", "android" ])
    workingDir = file("./app")
}

def addCordovaPlatformsTask = tasks.register("addCordovaPlatforms") {
    group("cordova")

    def androidSdkRoot = System.env.ANDROID_SKD_ROOT
    def addAndroidPlatform = androidSdkRoot != null && file(androidSdkRoot).exists()
    if (addAndroidPlatform) {
        dependsOn(addAndroidPlatformTask)
    }

    doFirst {
        if (!addAndroidPlatform) {
            logger.warn("skipped android-platform as ANDROID_SDK_ROOT does not exist")
        }
    }
}

tasks.register("initCordova") {
    group("build setup")
    dependsOn([createCordovaAppTask, addCordovaPlatformsTask])
}

tasks.register("checkCordovaRequirements", NpxTask) {
    group("cordova")

    command = "cordova"
    args = createCordovaNpxArgs("requirements")
    workingDir = file("./app")
    environment = getCordovaEnvironment()
}

def cordovaBuildTask = tasks.register("cordovaBuild", NpxTask) {
    group("build")

    it.inputs.dir("./app/platforms/android/cordova")
    it.inputs.dir("./app/platforms/android/CordovaLib")
    it.inputs.dir("./app/platforms/android/platform_www")
    it.inputs.file("./app/platforms/android/android.json")
    it.inputs.file("./app/platforms/android/build.gradle")
    it.inputs.file("./app/platforms/android/gradle.properties")
    it.inputs.file("./app/platforms/android/project.properties")
    it.inputs.file("./app/platforms/android/settings.gradle")

    it.inputs.dir("./app/plugins")
    it.inputs.dir("./app/www")
    it.inputs.file("./app/config.xml")
    it.inputs.file("./app/package.json")
    it.inputs.file("./app/package-lock.json")

    it.outputs.dir("./app/platforms/android/app/build")

    command = "cordova"
    args = createCordovaNpxArgs("build")
    workingDir = file("./app")
    environment = getCordovaEnvironment()
}

tasks.register("emulate", NpxTask).configure {
    group("run")
    command = "cordova"
    args = createCordovaNpxArgs(["emulate"])
    workingDir = file("./app")
    environment = getCordovaEnvironment()
}

tasks.register("serve", NpxTask).configure {
    group("run")

    command = "cordova"
    args = createCordovaNpxArgs("serve")
    workingDir = file("./app")
    environment = getCordovaEnvironment()
}

tasks.named("assemble").configure {
    dependsOn(cordovaBuildTask)
}

def getCordovaEnvironment() {
    def environment = [ JAVA_HOME: System.env.JAVA_HOME, ANDROID_SDK_ROOT: System.env.ANDROID_SDK_ROOT ]
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        return environment + [ Path: "${System.env.Path};${gradle.gradleHomeDir}\\bin;${System.env.ANDROID_SDK_ROOT}\\platform-tools;${System.env.ANDROID_SDK_ROOT}\\tools"]
    } else {
        return environment + [ PATH: "${System.env.PATH};${gradle.gradleHomeDir}/bin;${System.env.ANDROID_SDK_ROOT}/platform-tools;${System.env.ANDROID_SDK_ROOT}/tools"]
    }
}

def static createCordovaNpxArgs(args) {
    if (!(args instanceof Collection)) {
         args = [ args ]
    }
    return args + [ "--no-telemetry", "--no-install" ]
}
